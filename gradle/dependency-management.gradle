apply plugin: 'io.spring.dependency-management'

repositories {
    jcenter()
}

dependencyManagement {

    imports {
        mavenBom 'org.springframework:spring-framework-bom:4.3.8.RELEASE'
        mavenBom 'org.springframework.boot:spring-boot-starter-parent:1.5.3.RELEASE'
    }

    dependencies {
        dependency 'com.fasterxml.jackson.core:jackson-databind:2.8.8'

        dependency 'com.google.code.findbugs:jsr305:3.0.2'

        dependency 'com.google.guava:guava:21.0'

        dependency 'nf.fr.eraasoft:objectpool:1.1.2'

        dependency 'org.apache.commons:commons-lang3:3.5'

        dependencySet(group: 'org.bouncycastle', version: '1.55') {
            entry 'bcpkix-jdk15on'
            entry 'bcprov-jdk15on'
        }

        dependency 'org.codehaus.groovy:groovy-all:2.4.11'

        dependencySet(group: 'org.hamcrest', version: '1.3') {
            entry 'hamcrest-all'
            entry 'hamcrest-core'
            entry 'hamcrest-library'
        }

        dependencySet(group: 'org.slf4j', version: '1.7.25') {
            entry 'slf4j-api'
            entry 'jcl-over-slf4j'
        }

        dependencySet(group: 'org.spockframework', version: '1.1-groovy-2.4-rc-4') {
            entry 'spock-core'
            entry 'spock-spring'
        }
    }
}


DependencySubstitutions.metaClass.managedModule { String spec ->
    def managedVersion = dependencyManagement.managedVersions[spec]
    return delegate.module("$spec:$managedVersion")
}


configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module('commons-logging:commons-logging') with managedModule('org.slf4j:jcl-over-slf4j')
    }
}
