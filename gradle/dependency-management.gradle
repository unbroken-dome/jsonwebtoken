apply plugin: 'io.spring.dependency-management'

repositories {
    jcenter()
}

dependencyManagement {

    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:2.0.5.RELEASE'
    }

    dependencies {
        dependency 'com.google.code.findbugs:jsr305:3.0.2'

        dependency 'nf.fr.eraasoft:objectpool:1.1.2'

        dependencySet(group: 'org.bouncycastle', version: '1.60') {
            entry 'bcpkix-jdk15on'
            entry 'bcprov-jdk15on'
        }

        dependency 'org.codehaus.groovy:groovy-all:2.4.11'

        dependencySet(group: 'org.hamcrest', version: '1.3') {
            entry 'hamcrest-all'
            entry 'hamcrest-core'
            entry 'hamcrest-library'
        }

        dependencySet(group: 'org.slf4j', version: '1.7.25') {
            entry 'slf4j-api'
            entry 'jcl-over-slf4j'
        }

        dependencySet(group: 'org.spockframework', version: '1.1-groovy-2.4') {
            entry 'spock-core'
            entry 'spock-spring'
        }
    }
}


DependencySubstitutions.metaClass.managedModule { String spec ->
    def managedVersion = dependencyManagement.managedVersions[spec]
    return delegate.module("$spec:$managedVersion")
}


configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module('commons-logging:commons-logging') with managedModule('org.slf4j:jcl-over-slf4j')
    }
}
