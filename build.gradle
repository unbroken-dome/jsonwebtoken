plugins {
    id 'io.spring.dependency-management' version '1.0.6.RELEASE' apply false
    id 'com.jfrog.bintray' version '1.8.4' apply false
    id 'nebula.optional-base' version '4.0.1' apply false
    id 'org.unbroken-dome.test-sets' version '1.5.1' apply false
}


if (!gradle.startParameter.taskNames.contains('release')) {
    println "Not a release build, setting version to ${project.version}-SNAPSHOT"
    project.version += '-SNAPSHOT'
}


task 'release' {
    doLast {
        println "Releasing $version"
    }
}


subprojects { p ->
    p.version = rootProject.version

    p.plugins.withId('base') {
        p.apply from: "$rootDir/gradle/dependency-management.gradle"
    }

    p.plugins.withId('maven-publish') {
        p.apply from: "$rootDir/gradle/publishing.gradle"
    }

    p.plugins.withId('java') {
        p.apply from: "$rootDir/gradle/common-java.gradle"
    }
}
